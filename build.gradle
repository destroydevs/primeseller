plugins {
    id 'java'
    id 'me.champeau.jmh' version '0.7.2'
}

group = 'ru.destroy'
version = '1.0'

repositories {
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://libraries.minecraft.net/' }
}

dependencies {
    // configuration manager (https://github.com/Leymooo/AnnotatedYAMLConfiguration)
    implementation fileTree(dir: "depends", include: ['AnnotatedYAMLConfiguration-bukkit-1.2-SNAPSHOT.jar'])

    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'com.google.code.gson:gson:2.11.0:javadoc'
    compileOnly 'org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT'
    compileOnly 'com.mojang:authlib:1.5.25'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    implementation 'org.yaml:snakeyaml:2.3'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    runtimeOnly 'com.h2database:h2:1.4.190'
    implementation 'com.github.Anon8281:UniversalScheduler:0.1.6'

    jmhImplementation 'org.openjdk.jmh:jmh-core:1.37'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}